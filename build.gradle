plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id "me.champeau.jmh" version "0.6.6"
}

version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

loom {
	accessWidenerPath = file("src/main/resources/nicer_skies.accesswidener")
}

repositories {
	mavenCentral()
	maven { url "https://maven.terraformersmc.com/" } 	// modmenu
}

dependencies {
	// essentials & mappings
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	mappings loom.officialMojangMappings()

	// apis
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
	modRuntimeOnly include(fabricApi.module("fabric-command-api-v2", project.fabric_version))
	modRuntimeOnly include(fabricApi.module("fabric-lifecycle-events-v1", project.fabric_version))

	// devenv mods
	modRuntimeOnly("com.terraformersmc:modmenu:${project.modmenu_version}") { transitive = false }
	modRuntimeOnly fabricApi.module("fabric-screen-api-v1", project.fabric_version)
	modRuntimeOnly fabricApi.module("fabric-resource-loader-v0", project.fabric_version)
	modRuntimeOnly fabricApi.module("fabric-key-binding-api-v1", project.fabric_version)

	// libraries
	compileOnly "org.projectlombok:lombok:${project.lombok_version}"
	annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"

	// test
	testImplementation "org.junit.jupiter:junit-jupiter-api:${project.junit_version}"
	testImplementation "org.junit.jupiter:junit-jupiter-params:${project.junit_version}"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${project.junit_version}"
}

processResources {
	inputs.property "version", version // if version changes this task will be called again

	filesMatching("fabric.mod.json") {
		expand "version": version
	}
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}"}
	}
}
