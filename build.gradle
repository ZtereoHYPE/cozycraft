plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.8.0'	// to use genSourcesWithQuiltflower
	id "me.champeau.jmh" version "0.6.6"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "+" + project.minecraft_version
group = project.maven_group

repositories {
	mavenCentral()
	maven { url "https://maven.parchmentmc.org" }		// parchment mappings
	maven { url "https://maven.terraformersmc.com/" } 	// modmenu
	maven { name = "Modrinth"							// lazydfu, starlight, lithium
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
}

dependencies {
	// essentials & mappings
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	mappings loom.layered {
		it.officialMojangMappings()
//		it.parchment("org.parchmentmc.data:parchment-1.19.2:2022.08.10@zip") // https://ldtteam.jfrog.io/ui/native/parchmentmc-public/org/parchmentmc/data/parchment-1.18.1/BLEEDING-SNAPSHOT
	}

	// apis
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	// devenv mods
//	modRuntimeOnly "maven.modrinth:lithium:${project.lithium_version}"
//	modRuntimeOnly "maven.modrinth:spark:${project.spark_version}"
	modRuntimeOnly include(fabricApi.module("fabric-command-api-v2", project.fabric_version))
	modRuntimeOnly include(fabricApi.module("fabric-lifecycle-events-v1", project.fabric_version))
	modRuntimeOnly("com.terraformersmc:modmenu:${project.modmenu_version}") { transitive = false }
	modRuntimeOnly(include(fabricApi.module("fabric-screen-api-v1", project.fabric_version)))

	// libraries
	compileOnly 'org.projectlombok:lombok:1.18.24'
	annotationProcessor 'org.projectlombok:lombok:1.18.24'

	// test
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach { it.options.release = 17 }
java { withSourcesJar() }

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}
